; Based on: NSIS Modern User Interface/Basic Example Script by Joost Verburg

;--------------------------------
;Include Modern UI

  !include "MUI2.nsh"

;--------------------------------
;General

  ;Name and file
  Name "Spacetime #VERSION#"
  OutFile "Spacetime-#VERSION#.exe"

  ;Default installation folder
  InstallDir "$PROGRAMFILES\Spacetime"
  
  ;Get installation folder from registry if available
  InstallDirRegKey HKCU "Software\Spacetime" ""

  ;Request application privileges for Windows Vista
  RequestExecutionLevel user

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;Pages

;  !insertmacro MUI_PAGE_LICENSE "${NSISDIR}\Docs\Modern UI\License.txt"
;  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY
  !insertmacro MUI_PAGE_INSTFILES
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Installer Sections

Section

  SetOutPath "$INSTDIR"

  File python-dist\python.exe
  File python-dist\pythonw.exe
  File python-dist\python26.dll
  File /r /x .* python-dist\Lib

  File debug.bat

  CreateShortCut "$INSTDIR\Spacetime.lnk" "$INSTDIR\pythonw.exe" "-m spacetime.app"
  CreateShortCut "$INSTDIR\Spacetime (debug mode).lnk" "$INSTDIR\debug.bat"

  CreateDirectory "$SMPROGRAMS\Spacetime"
  CreateShortCut "$SMPROGRAMS\Spacetime\Spacetime.lnk" "$INSTDIR\pythonw.exe" "-m spacetime.app"
  CreateShortCut "$SMPROGRAMS\Spacetime\Spacetime (presentation mode).lnk" "$INSTDIR\pythonw.exe" "-m spacetime.app --presentation"
  CreateShortCut "$SMPROGRAMS\Spacetime\Spacetime (debug mode).lnk" "$INSTDIR\debug.bat"
  CreateShortCut "$SMPROGRAMS\Spacetime\Uninstall.lnk" "$INSTDIR\Uninstall.exe"

  SetOutPath "$INSTDIR\Lib\spacetime"
  File /x .* ..\lib\spacetime\*.py
  SetOutPath "$INSTDIR\Lib\spacetime\icons"
  File /x .* ..\lib\spacetime\icons\*.png
  
  ;Store installation folder
  WriteRegStr HKCU "Software\Spacetime" "" $INSTDIR

  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

SectionEnd

;--------------------------------
;Uninstaller Section

Section "Uninstall"

  Delete "$INSTDIR\python.exe"
  Delete "$INSTDIR\pythonw.exe"
  Delete "$INSTDIR\python26.dll"
  RMDir /r "$INSTDIR\Lib"

  RMDir /r "$SMPROGRAMS\Spacetime"
  
  Delete "$INSTDIR\Uninstall.exe"

  RMDir "$INSTDIR"

  DeleteRegKey /ifempty HKCU "Software\Spacetime"

SectionEnd
